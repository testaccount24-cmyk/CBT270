*          DATA SET CBT932     AT LEVEL 001 AS OF 02/25/79
* PSWSAMP           VERSION    1.0       WDPSC                          00001
* PSWSAMP SOURCE IS IN  WDPSC.DN00150.FILE(PSWSAMP)                     00002
*         IS THE SAMPLE TAKER FOR PACKLIST. THE TECHNIQUE IS TO MOVE    00003
* THE TRACE TABLE TO AN OUTPUT RECORD. IT DOES NOT MATTER THAT THE      00004
* MOVE LONG OF THE TABLE CAN BE INTERUPTED. THE VALUES TAKEN AT ANY     00005
* POINT ARE A SAMPLE, WHETHER INTERUPTED OR NOT.                        00006
* THE SAMPLE WILL NOT BEGIN UNLESS THE FIRST ENTRY OF THE TABLE IS      00007
* NOW DIFFERENT THAN THE PREVIOUS SAMPLE. THE SAMPLE RATE IS            00008
* APPROXIMATELY ONCE EVERY 2.5 SECONDS (THE FIELD HOW LONG CAN BE       00009
* ADJUSTED).                                                            00010
* THE TECHNIQUE FOR OVERALL SAMPLING AND END USE IS COVERED IN  THE     00011
* PACKLIST PROGRAM .                                                    00012
*                                                                       00013
* PACKLIST WILL RUN FOR THE TIME INTERVAL SET WITHIN THIS PROGRAM       00014
* AND THEN WILL AUTOMATICALLY TERMINATE.  IT CAN ALSO BE STOPPED BY     00015
* REPLYING TO THE WTOR ISSUED.  AT TERMINATION A MESSAGE IS ISSUED      00016
* SUPPLYING THE NAME OF THE TAPE USED TO COLLECT THE DATA.  THE TAPE    00017
* IS NOT CATALOGED SO WRITE ITS VOLSER DOWN FOR USE IN PACKLIST WHEN    00018
* THE OPERATOR CALLS.                                                   00019
*                                                                       00020
* INVOKING JCL     AVAILABLE IN 'SYS1.PROCLIB(PSWSAMP)'                 00021
* //PSWSAMP PROC                                                        00022
* //SAMPLE  EXEC PGM=PSWSAMP                                            00023
* //STEPLIB DD DSN=WDPSC.SOFTLOAD,DISP=SHR                              00024
* //TAPE DD DDNAME=IEFRDER                                              00025
* //IEFRDER DD DSN=PSWS,UNIT=TAPE,DISP=(,KEEP),LABEL=RETPD=15           00026
*                                                                       00027
         PRINT OFF                                                      00028
         MACRO                                                          00029
         REGISTER                                                       00030
         LCLA  &I                                                       00031
.LOOP    ANOP                                                           00032
R&I      EQU   &I                                                       00033
&I       SETA  &I+1                                                     00034
         AIF   (&I LT 16).LOOP                                          00035
         MEND                                                           00036
         PRINT ON                                                       00037
         TITLE  'PACKLIST   SAMPLE ROUTINE'                             00038
PSWSAMP CSECT                                                           00039
         REGISTER              REGISTER EQUATES                         00040
         SAVE   (14,12),,*     ENTRY SAVE                               00041
         LR    R12,R15         BASE REGISTER                            00042
         USING PSWSAMP,R12                                              00043
         LA    R8,SAVEAREA     SAVE  AREA                               00044
         ST    R8,8(,R13)      BACK CHAIN                               00045
         ST    R13,4(,R8)      FORWARD CHAIN                            00046
         LR    R13,R8                                                   00047
         TIME  BIN             TIME CHECK                               00048
         AL    R0,MAXTIME      MAX IS TIME TO RUN                       00049
         ST    R0,TIME         SAVE FOR REFERENCE                       00050
* MESSAGE TO OPERATOR SO PROGRAM CAN BE ENDED                           00051
         WTOR  '*** PSW ACTIVITY SAMPLER, ENTER STOP TO STOP',REPLY,   X00052
               L'REPLY,ECB,ROUTCDE=(2,11)                               00053
* CONTROL CONSTANTS                                                     00054
         L     R4,84           TRACE TABLE POINTER                      00055
         LM    R4,R5,4(R4)     ENTRIES IN TRACE TABLE                   00056
         SR    R5,R4        TRACE TABLE LENGTH IN R5                    00057
         STH   R5,TAPE+82     LRECL                                     00058
         STH   R5,TAPE+62     BLKSIZE                                   00059
         GETMAIN R,LV=(R5)     WORK AREA                                00060
         LR    R6,R1           WORK AREA                                00061
         LR    R7,R5           LENGTH                                   00062
         OPEN  (TAPE,(OUTPUT)) OUTPUT FILE                              00063
         STM   R4,R7,MOVLONG   SAVE REGISTERS FOR LOADING               00064
REWRITE  LM    R4,R7,MOVLONG   LOAD MOVE REGISTERS                      00065
         MVCL  R6,R4           MOVE DATA FROM TRACE TABLE TO OUTPUT     00066
         L     R6,MOVLONG+8    ADDRESS OF OUTPUT AREA                   00067
         PUT   TAPE,(R6)       EACH COPY IS SEPARATE RECORD             00068
         CLC   REPLY,STOP      OPERATOR STOPPING SAMPLE                 00069
         BE    GOHOME          YES                                      00070
         TIME  BIN             TIME CHECK                               00071
         CL    R0,TIME         HAS MAX TIME BEEN REACHED                00072
         BH    GOHOME          YES, THEN STOP                           00073
WHEN     EQU   *                                                        00074
         STIMER WAIT,MICVL=HOWLONG   WAIT A FEW SECONDS                 00075
         CLC   28(4,R4),28(R6)       HAS ENTIRE TRACE TABLE CHANGED     00076
         BNE   REWRITE               YES                                00077
         B     WHEN                  NO, WAIT SOME MORE                 00078
GOHOME   EQU   *                                                        00079
         L     R4,TAPE+44         GET DEB POINTER                       00080
         L     R5,32(,R4)         GET UCB ADDRESS                       00081
         MVC   ENDWTO-6(L'VOLSER),36(R5)   SAVE THE VOLSER              00082
         WTO   'PLEASE TELL SOFTWARE THAT PSWSAMP HAS BEEN STOPPED',  **00083
               ROUTCDE=(2,11)                                           00084
         LA    R1,WTOMSG                                                00085
         WTO   MF=(E,(1))      OUTPUT VOUME SERIAL NUMBER IN WTO        00086
         CLOSE TAPE            CLOSE THE OUTPUT FILE                    00087
         L     R13,SAVEAREA+4                                           00088
         LM    R14,R12,12(R13)                                          00089
         SLR   R15,R15                                                  00090
         BR    R14             RETURN                                   00091
SAVEAREA DS   18F                                                       00092
MOVLONG  DC    5F'0'                                                    00093
MAXTIME  DC    F'90000'                                                 00094
TIME     DC    F'0'                                                     00095
STOP     DC    C'STOP'                                                  00096
WTOMSG   WTO   'AND THE VOLSER OF THE TAPE IS 000000',MF=L              00097
ENDWTO   EQU   *                                                        00098
REPLY    DC    CL4' '                                                   00099
VOLSER   DS    CL6                                                      00100
ECB      DC    F'0'                                                     00101
WAITBLK  DC    F'0'                                                     00102
HOWLONG  DS    0D                                                       00103
         DC    X'0000000244400000'                                      00104
TAPE     DCB   DDNAME=TAPE,DSORG=PS,MACRF=PM,RECFM=F,BUFNO=1            00105
         END                                                            00106
