 /*                                                                  */ 00002
 /*   COPIED FROM THE CBT TAPE                                       */ 00002
 /*   SHOWS THE CALLING SEQUENCE FOR THE ENCIPHER/DECIPHER           */ 00002
 /*   PROGRAM CONTAINED IN MEMBER ENDECIPH                           */ 00002
 /*                                                                  */ 00002
        /* DATA SET TSO049     AT LEVEL 001 AS OF 01/23/81    */
 /* PROGRAM : TSO049                                                 */ 00001
 /*                                                                  */ 00002
 /*  PURPOSE:  TO ENCIPHER OR DECIPHER A DATASET BY USING THE NBS    */ 00003
 /*            ALGORITHM (R050A90).                                  */ 00004
 /*                                                                  */ 00005
 /*  JCL:   //STEP   EXEC   PGM=TSO049,PARM='OPERATION,KEY'          */ 00006
 /*         //DATASET DD    DSN=DSN.TO.BE.WORKED.ON,DISP=OLD         */ 00007
 /*         //SYSPRINT DD   SYSOUT=*                                 */ 00008
 /*                                                                  */ 00009
 /*  THE KEY IS A 1 TO 8 BYTE KEY FOR THE OPERATION                  */ 00010
 /*  OPERATION IS 'E' FOR ENCIPHER OR 'D' FOR DECIPHER               */ 00011
 /*                                                                  */ 00012
 /*  K TRUE   22 JAN 81   V1M0                                       */ 00013
 /*                                                                  */ 00014
 TSO049: PROC (PARAM) OPTIONS(MAIN) REORDER;                            00015
                                                                        00016
   DCL DATASET  FILE   RECORD  SEQUENTIAL ENV(CONSECUTIVE);             00017
   DCL SYSPRINT FILE   PRINT;                                           00018
                                                                        00019
   DCL PARAM    CHAR(100) VAR;                                          00020
   DCL PARM1    CHAR(1) INIT(''),                                       00021
       PARM2    CHAR(8) INIT(''),                                       00022
       PARM3    CHAR(8) INIT('');                                       00023
                                                                        00024
   DCL RECIN    CHAR(255) VAR INIT('');                                 00025
   DCL WORKREC  CHAR(255) INIT('');                                     00026
   DCL KEY      CHAR(8)   INIT('');                                     00027
   DCL OP  CHAR(12)  VAR INIT('');                                      00028
                                                                        00029
   DCL RECLEN   FIXED BIN(31,0) INIT(0);                                00030
   DCL WORKLEN  FIXED BIN(31,0) INIT(0);                                00031
   DCL RECCNT   FIXED BIN(31,0) INIT(0);                                00032
   DCL SKIPCNT  FIXED BIN(31,0) INIT(0);                                00033
   DCL (I,J,K)  FIXED BIN(15,0) INIT(0);                                00034
                                                                        00035
   DCL ENDFILE  BIT(1)  INIT('0'B);                                     00036
   DCL ENCIPHER BIT(1)  INIT('1'B);                                     00037
                                                                        00038
   DCL SETKEY   EXTERNAL ENTRY OPTIONS(ASSEMBLER,INTER);                00039
   DCL ENCIPH   EXTERNAL ENTRY OPTIONS(ASSEMBLER,INTER);                00040
   DCL DECIPH   EXTERNAL ENTRY OPTIONS(ASSEMBLER,INTER);                00041
                                                                        00042
   DCL (LENGTH,SUBSTR,VERIFY,INDEX)  BUILTIN;                           00043
                                                                        00044
                                                                        00045
1  ON ENDFILE(DATASET) ENDFILE = '1'B;                                  00046
                                                                        00047
   CALL SETUP;                                                          00048
                                                                        00049
   OPEN FILE(DATASET) UPDATE;                                           00050
   READ FILE(DATASET) INTO(RECIN);                                      00051
                                                                        00052
  /*  MAIN LOOP */                                                      00053
                                                                        00054
   DO WHILE(¬ENDFILE);                                                  00055
      RECCNT  = RECCNT + 1;         /*  BUMP RECORD COUNT        */     00056
      IF RECCNT > SKIPCNT THEN                                          00057
        DO;                                                             00058
          WORKREC = '';             /*  CLEAR OUT WORK AREA      */     00059
          WORKREC = RECIN;          /*  MOVE RECORD TO WORK AREA */     00060
          RECLEN = LENGTH(RECIN);   /*  SAVE LENGTH OF RECORD TOO*/     00061
          WORKLEN = RECLEN/8 * 8;   /*  ENSURE AT LEAST 8 BYTES  */     00062
                                                                        00063
          IF ENCIPHER THEN CALL ENCIPH(WORKREC,WORKLEN);                00064
                      ELSE CALL DECIPH(WORKREC,WORKLEN);                00065
                                                                        00066
          RECIN = SUBSTR(WORKREC,1,RECLEN); /* PUT RECORD BACK  */      00067
        END;                                                            00068
                                                                        00069
      REWRITE FILE(DATASET) FROM(RECIN);                                00070
      READ FILE(DATASET) INTO(RECIN);                                   00071
  END;                                                                  00072
                                                                        00073
  CLOSE FILE(DATASET);                                                  00074
  PUT FILE(SYSPRINT) SKIP EDIT                                          00075
     (OP,' OPERATION COMPLETED')       (A,A);                           00076
  PUT FILE(SYSPRINT) SKIP EDIT                                          00077
     (RECCNT,' RECORDS PROCESSED')     (P'ZZ,ZZZ,ZZ9',A);               00078
                                                                        00079
  IF SKIPCNT > 0 THEN                                                   00080
    PUT FILE(SYSPRINT) SKIP EDIT                                        00081
     (SKIPCNT,' RECORDS SKIPPED')      (P'ZZ,ZZZ,ZZ9',A);               00082
                                                                        00083
1/*     SUBROUTINE: SETUP                                  */           00084
 /* EXTRACT PARM INFORMATION AND SETUP FOR PROCESSING      */           00085
                                                                        00086
 SETUP: PROC;                                                           00087
                                                                        00088
                              /*                                    */  00089
                              /*        GET PARM1 &                 */  00090
                              /* PARM2 & PARM3 FROM EXEC PARAM      */  00091
                              /*                                    */  00092
    J=LENGTH(PARAM);                                                    00093
    IF J > 0 THEN I=INDEX(PARAM,',');                                   00094
    IF I=0 | J=0                                                        00095
       THEN DO;                                                         00096
          PARM1,PARM2,PARM3= '';                                        00097
       END;                                                             00098
       ELSE DO;                                                         00099
          PARM1=SUBSTR(PARAM,1,I-1);                                    00100
          I=I+1;                                                        00101
          K=INDEX(SUBSTR(PARAM,I),',');                                 00102
          IF K=0 | J = I                                                00103
             THEN DO;                                                   00104
                PARM2,PARM3= '';                                        00105
             END;                                                       00106
             ELSE DO;                                                   00107
                IF K = 0 THEN K = J + 1;                                00108
                PARM2=SUBSTR(PARAM,I,K-1);                              00109
                I=I+K;                                                  00110
                IF I <= J                                               00111
                   THEN PARM3=SUBSTR(PARAM,I);                          00112
             END;                                                       00113
       END;                                                             00114
                                                                        00115
    IF PARM1 = 'E'  THEN ENCIPHER = '1'B;                               00116
                    ELSE ENCIPHER = '0'B;                               00117
    IF ENCIPHER THEN OP = 'ENCIPHER';                                   00118
                ELSE OP = 'DECIPHER';                                   00119
                                                                        00120
    KEY = PARM2;                                                        00121
                                                                        00122
    IF PARM3 ¬= '       ' THEN /* SKIP NUMBER OF RECORDS         */     00123
      DO;                                                               00124
        K = VERIFY(PARM3,'01234567890 ');                               00125
        IF K = 0 THEN                                                   00126
          DO;                                                           00127
            SKIPCNT = PARM3;                                            00128
          END;                                                          00129
      END;                                                              00130
    PUT FILE(SYSPRINT) SKIP EDIT                                        00131
      (OP,' USING KEY=''',KEY,''' BEGINS')                              00132
      (A,A,A,A);                                                        00133
                                                                        00134
    CALL SETKEY(KEY);                                                   00135
    RETURN;                                                             00136
 END;                                                                   00137
 END TSO049;                                                            00138
