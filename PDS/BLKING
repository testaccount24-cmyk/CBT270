      *******************************                                        ***
       ID DIVISION.                                                          ...
      *******************************                                   05/27/80
                                                                        23521ALL
       PROGRAM-ID. BLKING.                                              01 LV003
       AUTHOR.     GARY DUFFIELD.                                       00006
       REMARKS.
                   THIS PROGRAM COMPUTES OPTIMUM BLKSIZE AND SPACE
             ALLOCATIONS FOR 3330, 3350 OR 3380 DISK PACKS.
           EJECT                                                        00007
      *******************************                                   00008
       ENVIRONMENT DIVISION.                                            00009
      *******************************                                   00010
                                                                        00011
       CONFIGURATION SECTION.

       SPECIAL-NAMES.
                                                                        00011
       INPUT-OUTPUT SECTION.                                            00012

       FILE-CONTROL.                                                    00013

           EJECT                                                        00015
      *******************************                                   00016
       DATA DIVISION.                                                   00017
      *******************************                                   00018
                                                                        00019
       FILE SECTION.                                                    00020
                                                                        00021
       WORKING-STORAGE SECTION.                                         00033
     5 77  PAN-VALET PICTURE X(24) VALUE '007CBT1536   10/27/89'.       00033
                                                                        00034
       01  SWITCHES.                                                    00035
           05  3330-BIG                 PIC XXX     VALUE 'NO '.        00036
               88  3330-EXCEEDED                    VALUE 'YES'.        00037
                                                                        00044
           05  3350-BIG                 PIC XXX     VALUE 'NO '.        00036
               88  3350-EXCEEDED                    VALUE 'YES'.        00037
                                                                        00044
           05  3380-BIG                 PIC XXX     VALUE 'NO '.        00036
               88  3380-EXCEEDED                    VALUE 'YES'.        00037
                                                                        00044
       01  WORK-AREAS.
           05  UNITTYPE                 PIC X(21)         VALUE SPACE.
           05  TRKCYL                   PIC XXX           VALUE 'TRK'.  00088
           05  BLOCKSIZE                PIC Z(4)9         VALUE ZERO.   00088
           05  PRIMARYA                 PIC Z(4)9         VALUE ZERO.   00088
           05  SECONDARYA               PIC Z(4)9         VALUE ZERO.   00088
           05  BLKSIZE                  PIC S9(9)  COMP-3 VALUE ZERO.   00088
           05  PRIMARY                  PIC S9(9)  COMP-3 VALUE ZERO.   00088
           05  SECONDARY                PIC S9(9)  COMP-3 VALUE ZERO.   00088
           05  TRKSINCYL                PIC S9(9)  COMP-3 VALUE ZERO.   00088
           05  MAXBLK                   PIC S9(9)  COMP-3 VALUE ZERO.   00088
           05  MAXSIZE                  PIC S9(9)  COMP-3 VALUE ZERO.   00088
           05  MAXSIZE1                 PIC S9(9)  COMP-3 VALUE ZERO.   00088
           05  RECINBLK                 PIC S9(9)  COMP-3 VALUE ZERO.   00088
           05  RECINTRK                 PIC S9(9)  COMP-3 VALUE ZERO.   00088
           05  NOOFTRKS                 PIC S9(9)  COMP-3 VALUE ZERO.   00088
           05  HOLDSIZE                 PIC S9(9)  COMP-3 VALUE ZERO.   00088
           05  NOOFCYLS                 PIC S9(9)  COMP-3 VALUE ZERO.   00088
           05  HOLDCYLS                 PIC S9(9)  COMP-3 VALUE ZERO.   00088
           05  BLKPERTRK                PIC S9(9)  COMP-3 VALUE ZERO.   00088
           05  BLKFCTR                  PIC S9(9)  COMP-3 VALUE ZERO.   00088
           05  OVERHEAD                 PIC S9(9)  COMP-3 VALUE ZERO.   00088
           05  TEST-BLKSIZE             PIC S9(9)  COMP-3 VALUE ZERO.   00088
           05  HOLD-RECSINTRK           PIC S9(9)  COMP-3 VALUE ZERO.   00088
           05  RECSINTRK                PIC S9(9)  COMP-3 VALUE ZERO.   00088
           05  BLKPERTRK1               PIC S9(9)  COMP-3 VALUE ZERO.   00088
           05  RECSINTRK1               PIC S9(9)  COMP-3 VALUE ZERO.   00088
               EJECT                                                    00238
      *******************************                                   00257
       LINKAGE SECTION.                                                 00258
      *******************************                                   00259

      * THE LRECL AND APPROXIMATE NUMBER OF RECORDS IN THE FILE ARE     00021
      * PASSED TO THE PROGRAM HERE.                                     00021
                                                                        00044
         01 PARM-FIELDS.
            05  PARM-LENGTH             PIC S9(4) COMP.
            05  LRECL                   PIC 9(7).
            05  FILESIZE                PIC 9(9).
               EJECT                                                    00238
      **************************************                            00239
       PROCEDURE DIVISION USING PARM-FIELDS.                            00240
      **************************************                            00241
       100-CONTROL-MODULE.                                              00242
      *******************************                                   00243
                                                                        00244
           PERFORM 300-PROCESS-PARMS-MODULE.                            00246
           STOP RUN.                                                    00255
           EJECT                                                        00256
      *******************************                                   00257
       300-PROCESS-PARMS-MODULE.                                        00258
      *******************************                                   00259
                                                                        00260
           DISPLAY '            UNIT TYPE          BLKSIZE   SPACE    ' 00260
                   ' PRIMARY      SECONDARY'
           DISPLAY '                                         UNIT'      00260
           IF LRECL > 13030                                             00261
                DISPLAY '     3330                        CANNOT BE'    00261
                        ' USED (LRECL TOO LARGE)'                       00261
                MOVE 'YES' TO 3330-BIG
           ELSE
                PERFORM 310-COMPUTE-3330.
           IF LRECL > 19069                                             00261
                DISPLAY '     3350                        CANNOT BE'    00261
                        ' USED (LRECL TOO LARGE)'                       00261
                MOVE 'YES' TO 3350-BIG
           ELSE
                PERFORM 320-COMPUTE-3350.
           IF LRECL > 32767                                             00261
                DISPLAY '     3380                        CANNOT BE'    00261
                        ' USED (LRECL TOO LARGE)'                       00261
                MOVE 'YES' TO 3380-BIG
           ELSE
                PERFORM 330-COMPUTE-3380.
           IF 3330-EXCEEDED OR 3350-EXCEEDED                            00261
                DISPLAY '     3330-3350 COMPATIBLE        (NOT'         00261
                        ' APPLICABLE)'                                  00261
           ELSE
                PERFORM 340-COMPUTE-3050.
           IF 3350-EXCEEDED OR 3380-EXCEEDED                            00261
                DISPLAY '     3350-3380 COMPATIBLE        (NOT'         00261
                        ' APPLICABLE)'                                  00261
           ELSE
                PERFORM 350-COMPUTE-5080.
                                                                        00260
      *******************************                                   00257
       310-COMPUTE-3330.                                                00258
      *******************************                                   00259
                                                                        00260
           MOVE '3330' TO UNITTYPE.
           MOVE     19 TO TRKSINCYL.                                    00260
           MOVE      0 TO HOLD-RECSINTRK.                               00260
           MOVE  13165 TO MAXSIZE.                                      00260
           MOVE    135 TO OVERHEAD.
           MOVE      0 TO BLKFCTR.                                      00260
           MOVE      0 TO TEST-BLKSIZE.                                 00260
           PERFORM 400-CHECK-BEST-MODULE                                00260
              UNTIL TEST-BLKSIZE > MAXSIZE.
           PERFORM 500-COMPUTE-MODULE.                                  00260
                                                                        00260
      *******************************                                   00257
       320-COMPUTE-3350.                                                00258
      *******************************                                   00259
                                                                        00260
           MOVE '3350' TO UNITTYPE.
           MOVE     30 TO TRKSINCYL.                                    00260
           MOVE      0 TO HOLD-RECSINTRK.                               00260
           MOVE  19254 TO MAXSIZE.                                      00260
           MOVE    185 TO OVERHEAD.
           MOVE      0 TO BLKFCTR.                                      00260
           MOVE      0 TO TEST-BLKSIZE.                                 00260
           PERFORM 400-CHECK-BEST-MODULE                                00260
              UNTIL TEST-BLKSIZE > MAXSIZE.
           PERFORM 500-COMPUTE-MODULE.                                  00260
                                                                        00260
      *******************************                                   00257
       330-COMPUTE-3380.                                                00258
      *******************************                                   00259
                                                                        00260
           MOVE '3380' TO UNITTYPE.
           MOVE     15 TO TRKSINCYL.                                    00260
           MOVE      0 TO HOLD-RECSINTRK.                               00260
           MOVE  47968 TO MAXSIZE.                                      00260
           MOVE    480 TO OVERHEAD.
           MOVE      0 TO BLKFCTR.                                      00260
           MOVE      0 TO TEST-BLKSIZE.                                 00260
           PERFORM 400-CHECK-BEST-MODULE                                00260
              UNTIL TEST-BLKSIZE > 32767.
           PERFORM 500-COMPUTE-MODULE.                                  00260
                                                                        00260
      *******************************                                   00257
       340-COMPUTE-3050.                                                00258
      *******************************                                   00259
                                                                        00260
           MOVE '3330-3350 COMPATIBLE' TO UNITTYPE.
           MOVE     19 TO TRKSINCYL.                                    00260
           MOVE      0 TO HOLD-RECSINTRK.                               00260
           MOVE  13165 TO MAXSIZE.                                      00260
           MOVE  19254 TO MAXSIZE1.                                     00260
           MOVE    185 TO OVERHEAD.
           MOVE      0 TO BLKFCTR.                                      00260
           MOVE      0 TO TEST-BLKSIZE.                                 00260
           PERFORM 420-CHECK-BEST-OF-BOTH-MODULE                        00260
              UNTIL TEST-BLKSIZE > MAXSIZE.
           PERFORM 500-COMPUTE-MODULE.                                  00260
                                                                        00260
      *******************************                                   00257
       350-COMPUTE-5080.                                                00258
      *******************************                                   00259
                                                                        00260
           MOVE '3350-3380 COMPATIBLE' TO UNITTYPE.
           MOVE     15 TO TRKSINCYL.                                    00260
           MOVE      0 TO HOLD-RECSINTRK.                               00260
           MOVE  19254 TO MAXSIZE.                                      00260
           MOVE  47968 TO MAXSIZE1.                                     00260
           MOVE    480 TO OVERHEAD.
           MOVE      0 TO BLKFCTR.                                      00260
           MOVE      0 TO TEST-BLKSIZE.                                 00260
           PERFORM 420-CHECK-BEST-OF-BOTH-MODULE                        00260
              UNTIL TEST-BLKSIZE > MAXSIZE.
           PERFORM 500-COMPUTE-MODULE.                                  00260
           EJECT                                                        00256
      *******************************                                   00257
       400-CHECK-BEST-MODULE.                                           00258
      *******************************                                   00259
                                                                        00260
           ADD 1 TO BLKFCTR
           COMPUTE TEST-BLKSIZE = (LRECL * BLKFCTR) + OVERHEAD.         00260
           IF TEST-BLKSIZE NOT > MAXSIZE                                00260
              PERFORM 410-COMPUTE-BEST-MODULE.                          00260
                                                                        00260
      *******************************                                   00257
       410-COMPUTE-BEST-MODULE.                                         00258
      *******************************                                   00259
                                                                        00260
           COMPUTE BLKPERTRK = MAXSIZE / TEST-BLKSIZE.                  00260
           COMPUTE RECSINTRK = BLKPERTRK * BLKFCTR.                     00260
           IF RECSINTRK > HOLD-RECSINTRK                                00260
              MOVE RECSINTRK      TO HOLD-RECSINTRK                     00260
              MOVE RECSINTRK      TO RECINTRK                           00260
              MOVE BLKFCTR        TO RECINBLK                           00260
              COMPUTE BLKSIZE = TEST-BLKSIZE - OVERHEAD.                00260
                                                                        00260
      *******************************                                   00257
       420-CHECK-BEST-OF-BOTH-MODULE.                                   00258
      *******************************                                   00259
                                                                        00260
           ADD 1 TO BLKFCTR.
           COMPUTE TEST-BLKSIZE =                                       00260
                   (LRECL * BLKFCTR) + OVERHEAD.                        00260
           IF TEST-BLKSIZE NOT > MAXSIZE                                00260
              PERFORM 430-COMPUTE-BEST-OF-BOTH.                         00260
                                                                        00260
      *******************************                                   00257
       430-COMPUTE-BEST-OF-BOTH.                                        00258
      *******************************                                   00259
                                                                        00260
           COMPUTE BLKPERTRK = MAXSIZE / TEST-BLKSIZE.                  00260
           COMPUTE RECSINTRK = BLKPERTRK * BLKFCTR.                     00260
           COMPUTE BLKPERTRK1 = MAXSIZE1 / TEST-BLKSIZE.                00260
           COMPUTE RECSINTRK1 = BLKPERTRK1 * BLKFCTR.                   00260
           IF RECSINTRK NOT < RECSINTRK1
              COMPUTE RECSINTRK =
                 (RECSINTRK + RECSINTRK1) - (RECSINTRK - RECSINTRK1)
           ELSE
              COMPUTE RECSINTRK =
                 (RECSINTRK1 + RECSINTRK) - (RECSINTRK1 - RECSINTRK).
           IF RECSINTRK > HOLD-RECSINTRK                                00260
              MOVE RECSINTRK      TO HOLD-RECSINTRK                     00260
              MOVE RECSINTRK      TO RECINTRK                           00260
              MOVE BLKFCTR        TO RECINBLK                           00260
              COMPUTE BLKSIZE = TEST-BLKSIZE - OVERHEAD.                00260
           EJECT                                                        00260
      *******************************                                   00257
       500-COMPUTE-MODULE.                                              00258
      *******************************                                   00259
                                                                        00260
           COMPUTE NOOFTRKS  = FILESIZE / RECINTRK.                     00260
           COMPUTE HOLDSIZE  = NOOFTRKS * RECINTRK.                     00260
           IF HOLDSIZE < FILESIZE                                       00260
                ADD 1 TO NOOFTRKS.                                      00260
           MOVE NOOFTRKS TO PRIMARY.                                    00260
           COMPUTE NOOFCYLS = NOOFTRKS / TRKSINCYL.                     00260
           IF NOOFCYLS > ZERO                                           00260
                MOVE 'CYL' TO TRKCYL                                    00260
                MOVE NOOFCYLS TO PRIMARY
                COMPUTE HOLDCYLS  = NOOFCYLS * TRKSINCYL                00260
                IF HOLDCYLS < NOOFTRKS                                  00260
                     ADD 1 TO PRIMARY.                                  00260
           COMPUTE SECONDARY  = PRIMARY / 4.                            00260
           COMPUTE PRIMARY  = PRIMARY - SECONDARY.                      00260
           IF SECONDARY = 0                                             00260
                MOVE 1 TO SECONDARY.                                    00260
           MOVE BLKSIZE TO BLOCKSIZE.
           MOVE PRIMARY TO PRIMARYA.
           MOVE SECONDARY TO SECONDARYA.
           DISPLAY '     ' UNITTYPE '     ' BLOCKSIZE '     ' TRKCYL    00260
                '      ' PRIMARYA '         ' SECONDARYA                00260
           MOVE ZERO TO BLKSIZE PRIMARY SECONDARY TRKSINCYL MAXBLK      00260
                RECINBLK RECINTRK NOOFTRKS HOLDSIZE NOOFCYLS TRKSINCYL  00088
                HOLDCYLS.                                               00088
           MOVE SPACE TO UNITTYPE.
           MOVE 'TRK' TO TRKCYL.
